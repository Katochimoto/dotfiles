<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<!-- ############################################################################################################## -->

<xsl:output method="xml" encoding="utf-8"/>

<!-- ############################################################################################################## -->

<xsl:template match="handler">
    <xsl:copy>
        <xsl:copy-of select="@*"/>
        <xsl:apply-templates select="body"/>
    </xsl:copy>
</xsl:template>

<!-- ############################################################################################################## -->

<xsl:template match="body">
    <settings>
        <xsl:apply-templates select="color_scheme | quotation_char | signature | default_mailbox | from_name
            | messages_per_page | quotation_char | page_after_send | page_after_delete | page_after_move
            | subs_messages_per_page | imap_status | abook_page_size | label_sort | reply_to | default_email" mode="value"/>
        <xsl:apply-templates select="folder_thread_view | enable_quoting | enable_richedit
            | enable_firstline | enable_social_notification | show_advertisement | show_news
            | show_avatars | enable_quoting | yandex_sign_enable | collect_addresses | signature_top
            | save_sent | enable_autosave | pop_spam_subject_mark_enable | pop3_makes_read
            | suggest_addresses | hide_daria_header | use_small_fonts | show_chat | use_monospace_in_text" mode="on-off"/>
    </settings>
</xsl:template>

<xsl:template match="*" mode="value">
    <xsl:copy-of select="."/>
</xsl:template>

<xsl:template match="color_scheme[. = '']" mode="value">
    <color_scheme>blue</color_scheme>
</xsl:template>

<xsl:template match="*" mode="on-off"/>

<xsl:template match="*[. = 'on']" mode="on-off">
    <xsl:element name="{name()}"/>
</xsl:template>

<!-- ############################################################################################################## -->

</xsl:stylesheet>


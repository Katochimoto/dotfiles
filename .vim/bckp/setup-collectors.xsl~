<?xml version="1.0" encoding="utf-8"?>

<!DOCTYPE xsl:stylesheet [
    <!ENTITY % blocks.ent SYSTEM "../../blocks.ent"> %blocks.ent;
]>

<xsl:stylesheet version="1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    >

<!-- ############################################################################################################## -->

<xsl:template match="setup-collectors" mode="block-content">
    <xsl:param name="id" select="generate-id(.)"/>
<div class="b-layout__inner">
    <div class="b-setup">
        <div class="b-setup__inner">

            <xsl:apply-templates select="/" mode="setup-title-breadcrumbs"/>

            <div class="b-pop">
                <p class="b-pop__description">
                    <xsl:value-of select="i18n('%Setup_Collectors_description')"/>
                </p>
                <!-- Подключённые сборщики -->
                <xsl:apply-templates select="/page/collectors"/>
            </div>

            <div class="b-form-layout b-form-layout_collectors">
                <form class="b-form-layout__form daria-form" method="POST" action="collector.create">
                    <!-- Форма ввода логина и пароля -->
                    <div class="b-form-layout__block">
                        <div class="b-setup-title">
                            <xsl:value-of select="i18n('%Setup_Collectors_Забирать_почту_из_ящика')"/>
                        </div>

                        <xsl:apply-templates select="." mode="setup-collector-server-field-text">
                            <xsl:with-param name="id" select="$id"/>
                            <xsl:with-param name="name" select="'email'"/>
                            <xsl:with-param name="label-text" select="'E-mail'"/>
                        </xsl:apply-templates>
                        <xsl:apply-templates select="." mode="setup-collector-server-field-text">
                            <xsl:with-param name="id" select="$id"/>
                            <xsl:with-param name="name" select="'password'"/>
                            <xsl:with-param name="label-text" select="'Пароль'"/>
                        </xsl:apply-templates>
                        <xsl:apply-templates select="." mode="setup-collector-server-field-checkbox">
                            <xsl:with-param name="id" select="$id"/>
                            <xsl:with-param name="name" select="'no_delete_msg'"/>
                            <xsl:with-param name="checked" select="."/>
                            <xsl:with-param name="label-text" select="'Сохранять оригиналы писем в ящике'"/>
                        </xsl:apply-templates>
                    </div>

                    <!-- Форма дополнительных параметров сервера -->
                    <div class="b-form-layout__block b-form-layout__block_server-settings g-hidden">
                        <div class="b-setup-title">
                            <xsl:value-of select="i18n('%Setup_Collectors_Параметры_сервера')"/>
                        </div>

                        <xsl:apply-templates select="." mode="setup-collector-server-field-text">
                            <xsl:with-param name="id" select="$id"/>
                            <xsl:with-param name="name" select="'login'"/>
                            <xsl:with-param name="label-text" select="'Логин'"/>
                        </xsl:apply-templates>
                        <xsl:apply-templates select="." mode="setup-collector-server-field-text">
                            <xsl:with-param name="id" select="$id"/>
                            <xsl:with-param name="name" select="'server'"/>
                            <xsl:with-param name="label-text" select="'Сервер'"/>
                        </xsl:apply-templates>
                        <xsl:apply-templates select="." mode="setup-collector-server-field-text">
                            <xsl:with-param name="id" select="$id"/>
                            <xsl:with-param name="name" select="'port'"/>
                            <xsl:with-param name="label-text" select="'Порт'"/>
                        </xsl:apply-templates>
                        <xsl:apply-templates select="." mode="setup-collector-server-field-checkbox">
                            <xsl:with-param name="id" select="$id"/>
                            <xsl:with-param name="name" select="'use_ssl'"/>
                            <xsl:with-param name="label-text" select="'Собирать почту, используя протокол шифрования SSL'"/>
                        </xsl:apply-templates>
                    </div>

                    <div class="b-form-layout__block b-form-layout__block_submit">
                        <div class="b-form-layout__line">
                            <div class="b-form-layout__field">
                                <span class="b-form-element">
                                    <input type="submit" class="b-form-element__button" value="{i18n('%Setup_Collectors_Включить_сборщик')}"/>
                                </span>
                            </div>
                            <xsl:apply-templates select="." mode="setup-collector-action-status">
                                <xsl:with-param name="name" select="'create'"/>
                                <xsl:with-param name="type" select="'loading'"/>
                                <xsl:with-param name="text" select="'Проверка соединения...'"/>
                            </xsl:apply-templates>
                            <xsl:apply-templates select="." mode="setup-collector-action-status">
                                <xsl:with-param name="name" select="'create'"/>
                                <xsl:with-param name="type" select="'fail'"/>
                                <xsl:with-param name="text" select="'Указанный сервер не существует или к нему невозможно подключиться.'"/>
                            </xsl:apply-templates>
                            <xsl:apply-templates select="." mode="setup-collector-action-status">
                                <xsl:with-param name="name" select="'create'"/>
                                <xsl:with-param name="type" select="'auth_failed'"/>
                                <xsl:with-param name="text" select="'Сервер не отвечает, либо введен неверный логин или пароль.'"/>
                            </xsl:apply-templates>
                            <div class="b-form-layout__field b-notification_status-duplicated_create g-hidden">
                                <!--FIXME: локализовать, когда будут переменные-->
                                <span class="b-notification ">
                                    <span class="b-notification__i">
                                        <img  title="" alt="[!]" class="b-mail-icon b-mail-icon_error"><xsl:attribute name="src"><xsl:value-of select="'&static;/blocks/b-mail-icon/_type/b-mail-icon_error.gif'"/></xsl:attribute></img>
                                        <xsl:text> Сборщик почты с сервера </xsl:text>
                                        <span class="server"></span>
                                        <xsl:text> для логина </xsl:text>
                                        <span class="login"></span>
                                        <xsl:text> уже существует.</xsl:text>
                                    </span>
                                </span>
                            </div>
                        </div>
                    </div>
                </form>
            </div>
        </div>
    </div>
</div>
</xsl:template>

<!-- ############################################################################################################## -->

<!-- Таблица со сборщиками -->
<xsl:template match="collectors">
    <xsl:if test="items/item">
        <div class="b-pop__header">
            <xsl:value-of select="i18n('%Setup_Collectors_Подключенные_ящики')"/>
        </div>
        <table class="b-pop__box">
            <xsl:apply-templates select="items/item"/>
        </table>
    </xsl:if>
</xsl:template>

<!-- Сборщик -->
<xsl:template match="collectors/items/item">
    <xsl:variable name="popid" select="popid"/>
    <xsl:variable name="filters" select="/page/filters/body/action[conditions/item/pattern=$popid]"/>

    <tbody class="b-pop__email">
        <tr>
            <!-- Название ящика для сборщика -->
            <td class="b-pop__email-email">
                <span class="b-pop__email-value daria-action" action="collector.toggle-extra-info">
                    <xsl:choose>
                        <xsl:when test="email != ''">
                            <xsl:value-of select="email"/>
                        </xsl:when>
                        <xsl:otherwise>
                            <xsl:value-of select="login"/>
                        </xsl:otherwise>
                    </xsl:choose>
                </span>
            </td>
            <!-- Тумблер сборщика -->
            <td class="b-pop__email-state">
                <span>
                    <xsl:attribute name="class">
                        <xsl:text>b-switch</xsl:text>
                        <xsl:apply-templates select="." mode="rpopper-state-class"/>
                    </xsl:attribute>

                    <span class="b-switch-off daria-action" action="collector.off" params="popid={popid}">
                        <xsl:value-of select="i18n('%Setup_выкл')"/>
                        <img alt=""  class="b-switch-off-img"><xsl:attribute name="src"><xsl:value-of select="'&static;/blocks/b-switch/b-switch-on.png'"/></xsl:attribute></img>
                    </span>
                    <span class="b-switch-on daria-action" action="collector.on" params="popid={popid}">
                        <img alt=""  class="b-switch-on-img"><xsl:attribute name="src"><xsl:value-of select="'&static;/blocks/b-switch/b-switch-off.png'"/></xsl:attribute></img>
                        <xsl:value-of select="i18n('%Setup_вкл')"/>
                    </span>
                </span>
            </td>
            <td>
                <xsl:apply-templates select="error_status" mode="collector-list-error"/>
            </td>
        </tr>
        <!-- Расширенная информация о сборщике -->
        <tr>
            <td class="b-pop__email-setup" colspan="3">
                <div class="b-pop__email-setup-wrapper">
                    <div class="b-pop__email-setup-text">
                        <!-- Ошибки -->
                        <xsl:apply-templates select="error_status" mode="collector-error"/>

                        <!-- Оставлять письма на сервере -->
                        <xsl:if test="no_delete_msg = ''">
                            <xsl:value-of select="i18n('%Setup_Collectors_Удалять_оригиналы')"/><br/>
                        </xsl:if>

                        <!-- Последняя проверка -->
                        <xsl:choose>
                            <!--FIXME: локализовать, когда будут переменные-->
                            <xsl:when test="last_connect != ''">
                                <xsl:text>Последняя проверка была </xsl:text>
                                <xsl:value-of select="human_time"/>
                                <xsl:text>.</xsl:text>
                            </xsl:when>
                            <xsl:otherwise>
                                <xsl:value-of select="i18n('%Setup_Collectors_Соединение_установлено')"/><br/>
                            </xsl:otherwise>
                        </xsl:choose><br/>

                        <!-- Присвоить метку -->
                        <xsl:apply-templates select="$filters[action='movel']" mode="collector-extra-info-label"/>
                        <!-- Переместить в папку -->
                        <xsl:apply-templates select="$filters[action='move']" mode="collector-extra-info-folder"/>
                    </div>

                    <!--
                    <a class="b-pop__email-setup-link daria-action" href=""
                        action="collector.open-settings" params="popid={popid}&amp;email={email}">Настроить…</a>
                    <a class="b-pop__email-setup-link daria-action" href=""
                        action="collector.remove" params="popid={popid}&amp;email={email}">Удалить</a>
                    -->
                    <a class="b-pop__email-setup-link daria-action" href="" action="collector.open-settings" params="popid={popid}">
                        <xsl:value-of select="i18n('%Setup_Collectors_Настроить')"/>
                    </a>
                    <a class="b-pop__email-setup-link daria-action" href="" action="collector.remove" params="popid={popid}&amp;email={email}">
                        <xsl:value-of select="i18n('%Setup_Collectors_Удалить')"/>
                    </a>
                </div>
            </td>
        </tr>
    </tbody>
</xsl:template>

<!-- Модификатор тумблера сборщика -->
<xsl:template match="item" mode="rpopper-state-class"/>
<xsl:template match="item[enable='0']" mode="rpopper-state-class">
    <xsl:text> b-switch_off</xsl:text>
</xsl:template>
<xsl:template match="item[enable='1']" mode="rpopper-state-class">
    <xsl:text> b-switch_on</xsl:text>
</xsl:template>

<!-- Общая ошибка выводится в списке сборщиков рядом с ним, если он не работает правильно -->
<xsl:template match="error_status" mode="collector-list-error"/>
<xsl:template match="error_status[text()]" mode="collector-list-error">
    <img  title="" alt="[!]" class="b-mail-icon b-mail-icon_error"><xsl:attribute name="src"><xsl:value-of select="'&static;/blocks/b-mail-icon/_type/b-mail-icon_error.gif'"/></xsl:attribute></img>
    <xsl:value-of select="i18n('%Setup_Collectors_Произошла_ошибка')"/>
</xsl:template>
<xsl:template match="error_status[text() = 'ok']" mode="collector-list-error"/>

<!-- ############################################################################################################## -->

<!-- Вывод экста информации сборщика -->

<!-- Ошибки -->
<xsl:template match="error_status" mode="collector-error"/>
<xsl:template match="error_status[text()]" mode="collector-error">
    <xsl:variable name="server" select="../server"/>
    <xsl:choose>
        <!--FIXME: локализовать, когда будут переменные-->
        <xsl:when test="text() = 'Error while connecting to the server'">
            <xsl:text>Ошибка соединения с сервером. Пожалуйста, повторите попытку через некоторое время. </xsl:text>
            <xsl:text>Если ошибка регулярно повторяется, напишите в службу поддержки</xsl:text>
            <xsl:apply-templates select="." mode="collector-error-support-link">
                <xsl:with-param name="server" select="$server"/>
            </xsl:apply-templates>
        </xsl:when>
        <!--FIXME: локализовать, когда будут переменные-->
        <xsl:when test="text() = 'Authentication error'">
            <xsl:text>Ошибка авторизации. Пожалуйста, проверьте логин и пароль. </xsl:text>
            <xsl:text>Если ошибка повторяется, напишите в службу поддержки</xsl:text>
            <xsl:apply-templates select="." mode="collector-error-support-link">
                <xsl:with-param name="server" select="$server"/>
            </xsl:apply-templates>
        </xsl:when>
        <!--FIXME: локализовать, когда будут переменные-->
        <xsl:when test="text() = 'The operation timed out'">
            <xsl:text>Сервер не отвечает. Пожалуйста, повторите попытку через некоторое время. </xsl:text>
            <xsl:text>Если ошибка воспроизводится, необходимо написать в службу поддержки</xsl:text>
            <xsl:apply-templates select="." mode="collector-error-support-link">
                <xsl:with-param name="server" select="$server"/>
            </xsl:apply-templates>
        </xsl:when>
        <xsl:when test="text() = 'Mailbox size limit is exceed'">
            <xsl:value-of select="i18n('%Setup_Collectors_Ящик_переполнен')"/>
        </xsl:when>
        <!--FIXME: локализовать, когда будут переменные-->
        <xsl:when test="text() = 'Error resolving server address'">
            <xsl:text>Сервера с именем </xsl:text>
            <xsl:value-of select="$server"/>
            <xsl:text> не существует, либо он не отвечает. Если Вы уверены, что имя введено правильно, </xsl:text>
            <xsl:text>а ошибка воспроизводится, пожалуйста, сообщите об этом в саппорт сервера, </xsl:text>
            <xsl:text>с которого хотите забирать почту</xsl:text>
        </xsl:when>
        <xsl:when test="text() = 'Server did not initiated the connection correctly'">
            %i18n_Setup_Collectors_Некорректный_ответ_сервера;
        </xsl:when>
        <xsl:otherwise>
            %i18n_Setup_Collectors_Ошибка_соединения;
        </xsl:otherwise>
    </xsl:choose>
    <xsl:text>.</xsl:text><br/>
</xsl:template>
<xsl:template match="error_status[text() = 'ok']" mode="collector-error"/>

<!-- Печатает в чей именно саппорт обращаться -->
<xsl:template match="error_status" mode="collector-error-support-link">
    <xsl:param name="server"/>
    <xsl:choose>
        <xsl:when test="$server='pop.yandex.ru'">
            <xsl:text> пользователей </xsl:text>
            <a href="mailto:support@yandex-team.ru">Яндекс.Почты</a>
        </xsl:when>
        <xsl:otherwise>
            <xsl:text> сервера </xsl:text>
            <xsl:value-of select="$server"/>
        </xsl:otherwise>
    </xsl:choose>
</xsl:template>

<!-- Письмам сборщика присвоена метка -->
<!--FIXME: локализовать, когда будут переменные-->
<xsl:template match="action" mode="collector-extra-info-label">
    <xsl:variable name="id" select="param"/>
    <xsl:text>Присваивать собранным письмам метку "</xsl:text>
    <xsl:value-of select="$labels/label[@id=$id]/name"/>
    <xsl:text>".</xsl:text><br/>
</xsl:template>

<!-- Письма сборщика перекладываются в папку -->
<!--FIXME: локализовать, когда будут переменные-->
<xsl:template match="action" mode="collector-extra-info-folder">
    <xsl:text>Помещать письма в папку "</xsl:text>
    <xsl:value-of select="param"/>
    <xsl:text>".</xsl:text><br/>
</xsl:template>

<!-- ############################################################################################################## -->

</xsl:stylesheet>

